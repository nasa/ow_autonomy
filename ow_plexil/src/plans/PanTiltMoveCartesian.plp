// The Notices and Disclaimers for Ocean Worlds Autonomy Testbed for Exploration
// Research and Simulation can be found in README.md in the root directory of
// this repository.

// Point the antenna at the specified coordinate using the specified frame.

#include "plan-interface.h"

PanTiltMoveCartesian:
{
  In Integer Frame;
  In Real X, Y, Z;

  Boolean FaultDetected = false;

  PostCondition !Lookup(AntennaFault);

  if Lookup(AntennaFault)
  { 
    log_error ("Command pan_tilt_cartesian not sent to lander ",
               "due to active antenna fault(s).");
    FaultDetected = true;
  }

  SendPanTiltCartesian:
  {
    Start !Lookup(AntennaFault);

    if FaultDetected
    {
      log_info ("Antenna fault(s) resolved, ",
                "sending pan_tilt_cartesian command to lander...");
    }

    SynchronousCommand pan_tilt_cartesian (Frame, X, Y, Z);
  }

}
