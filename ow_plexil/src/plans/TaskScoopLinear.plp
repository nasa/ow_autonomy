// The Notices and Disclaimers for Ocean Worlds Autonomy Testbed for Exploration
// Research and Simulation can be found in README.md in the root directory of
// this repository.

// Perform one pass of a linear scooping action.

#include "plan-interface.h"

TaskScoopLinear:
{
  In Integer Frame;
  In Boolean Relative;
  In Real X;
  In Real Y;
  In Real Z;
  In Real Depth;
  In Real Length;

  Boolean FaultDetected = false;

  PostCondition !Lookup(ArmFault);

  if Lookup(ArmFault)
  {
    log_error ("Command scoop_linear not sent to lander due to active arm fault(s).");
    FaultDetected = true;
  }

  SendTaskScoopLinear:
  {
    Start !Lookup(ArmFault);

    if FaultDetected
    {
      log_info ("Arm fault(s) resolved, sending scoop_linear command to lander...");
    }

    SynchronousCommand scoop_linear (Frame, Relative, X, Y, Z, Depth, Length);
  }

}
