// The Notices and Disclaimers for Ocean Worlds Autonomy Testbed for Exploration
// Research and Simulation can be found in README.md in the root directory of
// this repository.

// This plan removes the tailings from a trench that assumed to have been dug
// using given X, Y, and GroundPos.  It deposits them in a predecided location.

#include "ow-interface.h"

RemoveTailings:
{
  In Real X;
  In Real Y;
  In Real GroundPos;
  In Boolean Parallel;

  // NOTE: These values selected only to save time during testing, as each pass
  // takes several minutes.
  Real bite_depth = 0.03;
  Integer num_passes = 2;

  for (Integer pass = 1; pass <= num_passes; pass + 1) {
    log_info ("Removing tailings, pass ", pass, " of ", num_passes, "...");
    LibraryCall TaskScoopCircular (Frame = 0, Relative = false,
                                   X = X, Y = Y, Z = GroundPos,
                                   Depth = bite_depth * pass,
                                   Parallel = Parallel);


    // NOTE: these X/Y/Z values specify a predecided dump location
    // that works in any of the provided simulation worlds.  A real
    // mission procedure would need to find an appropriate dump
    // location.
    LibraryCall TaskDiscardSample (Frame = 0, Relative = false,
                                   Point = #(1.5 0.8 0.4), Height = 0.0);
  }
}
