// The Notices and Disclaimers for Ocean Worlds Autonomy Testbed for Exploration
// Research and Simulation can be found in README.md in the root directory of
// this repository.
 
// Unstow and stow the arm repeatedly. Pan the antenna
// in 15 degree increments when 'Unstow' action goal status
// is 'ACTIVE' and pan the antenna in 15 degree decrements 
// when 'Stow' action goal status is 'ACTIVE'. 
// This plan will be terminated if any fault occurs.

#include "plan-interface.h"

ActionGoalStateDemo:
{
  Real NewAngle = 0;

  log_info ("Starting ActionGoalStateDemo plan...");

  Run: Concurrence
  {
    ExitCondition Lookup(SystemFault);

    Pattern2Image:
    {
      RepeatCondition true;
      StartCondition !Lookup(AntennaFault);

      if (Lookup(ActionGoalStatus("Stow")) == 1)
      {
        NewAngle = (Lookup(PanDegrees) - 15) mod 360;
      }
      else NewAngle = (Lookup(PanDegrees) + 15) mod 360;
  
      LibraryCall Pan (Degrees=NewAngle);
    }

    Pattern2UnstowStow:
    {
      RepeatCondition true;
      StartCondition !Lookup(ArmFault);
      InvariantCondition !Lookup(ArmFault);

      LibraryCall Unstow();
      LibraryCall Stow();
    }
  }
}
